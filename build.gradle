apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'antlr'
apply plugin: 'idea'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.sedovalx.gradle:gradle-aspectj-binary:1.0.25"
    }
}

allprojects {
	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

apply plugin: 'com.github.sedovalx.gradle-aspectj-binary'

ext {
	jdk = System.env.'JAVA_HOME'
	aspectjVersion = '1.8.13'
    weave = false
}

repositories {
	mavenCentral()
	jcenter()
	mavenLocal()
}

configurations {
	antlr
}

clean {
    delete 'generated-src/'
	delete 'bundles/'
}

compileJava {
    // Enable AspectJ-based thread auditor when '-PthreadAudit' is included on command line
    if (project.hasProperty('threadAudit')) {
        weaveClasses.dependsOn compileJava
        classes.dependsOn weaveClasses
    }
}

sourceSets {
	main.java.srcDirs += 'generated-src/main/java'
}

idea {
	module {
		sourceDirs += file('generated-src/main/java')
	}
}

dependencies {
	antlr "org.antlr:antlr4:4.7.1"

	compile 'com.intellij:javac2:7.0.3'
	compile 'com.google.code.gson:gson:2.8.1'
	compile 'com.google.guava:guava:19.0'
	compile ('com.defano.jmonet:jmonet:0.2.2') {changing = false}
	compile ('com.defano.jsegue:jsegue:0.0.1') {changing = false}
	compile 'org.bitbucket.cowwoc.diff-match-patch:diff-match-patch:1.0'
	compile 'com.l2fprod.common:l2fprod-common-fontchooser:6.9.1'
	compile group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: '5.2'
	compile group: 'de.dfki.mary', name: 'voice-cmu-bdl-hsmm', version: '5.2'
	compile group: 'de.dfki.mary', name: 'voice-cmu-rms-hsmm', version: '5.2'
	compile group: 'de.dfki.mary', name: 'voice-dfki-prudence-hsmm', version: '5.2'
	compile "org.aspectj:aspectjrt:$aspectjVersion"

	testCompile 'junit:junit:4.12'
	testCompile group: 'commons-io', name: 'commons-io', version: '2.4'

}

generateGrammarSource {
	outputDirectory = file("generated-src/main/java/")
	maxHeapSize = "64m"
	arguments += ["-long-messages", "-no-listener", "-visitor", "-package", "com.defano.hypertalk.parser"]
}

jar {
	exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

	from {
		(configurations.runtime).collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	manifest {
		attributes 'Main-Class': 'com.defano.wyldcard.WyldCard'
	}
}

task generateBundle(type: Exec) {
	dependsOn build
	workingDir "${project.projectDir.absolutePath}"
	commandLine "${jdk}/bin/javapackager",
			"-deploy",
			"-native",
			"-outdir", ".",
			"-outfile", "WyldCard.app",
			"-srcfiles", "./build/libs/wyldcard.jar",
            "-appclass", "com.defano.wyldcard.WyldCard",
			"-name", "WyldCard",
			"-title", "WyldCard"
}

mainClassName="com.defano.wyldcard.WyldCard"
